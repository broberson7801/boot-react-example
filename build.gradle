plugins {
	id 'org.springframework.boot' version '2.2.1.RELEASE'
	id 'io.spring.dependency-management' version '1.0.8.RELEASE'
	id 'java'
	id "com.moowork.node" version "1.3.1"
}

group = 'com.options.clearing.corporation'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
	mavenCentral()
}

/*
	we only install and download node on the "web" profile;
	the below check makes the "commandline" build much more lean and quicker to build
 */
if(System.properties['spring.profiles.active'] == 'web') {
	println('web profile detected, loading node')
	node {
		download = true

		// Set the work directory for unpacking node
		workDir = file("${project.buildDir}/nodejs")

		// Set the work directory for NPM
		npmWorkDir = file("${project.buildDir}/npm")
	}

	task appNpmInstall(type: NpmTask) {
		description = "Installs all dependencies from package.json"
		workingDir = file("${project.projectDir}/src/main/webapp")
		args = ["install"]
	}

	task appNpmBuild(type: NpmTask) {
		description = "Builds production version of the webapp"
		workingDir = file("${project.projectDir}/src/main/webapp")
		args = ["run", "build"]
	}

	task copyWebApp(type: Copy) {
		from 'src/main/webapp/build'
		into 'build/resources/main/static/'
	}

	appNpmBuild.dependsOn appNpmInstall
	copyWebApp.dependsOn appNpmBuild
	compileJava.dependsOn copyWebApp
}

dependencies {

	implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: '2.2.1.RELEASE'
	implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.6'

	testImplementation('org.springframework.boot:spring-boot-starter-test')
	testImplementation group: 'org.powermock', name: 'powermock-api-mockito2', version: '2.0.4'

}

test {
	useJUnitPlatform()
}

bootRun {

	// we set standard in here as the scanner will throw a "no such element" exception without it
	standardInput = System.in
	String activeProfile =  System.properties['spring.profiles.active']
	systemProperty "spring.profiles.active", activeProfile
}

